/*
Advanced State Machine v1.0 by Edward Newton (https://github.com/aguynamededward/gms2.3_adv_state_machine/)
Functional as of Gamemaker Studio 2 Runtime v2.3.3.437	
	
Bugs or other issues: twitter: @aguynamededward
	

MIT License

Copyright (c) 2021 Edward Newton (github.com/aguynamededward)

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/



/*

TEMPLATE STATE MACHINE CODE 
is at the bottom of this document.


SETTING UP ENUMS FOR THE STATE MACHINE
It's much easier to use the state machine with enums, and you can set up the sub-state enums at the same time.

Uncomment the code below and the enum will be set up when your game runs.

*/


/* 

enum state { // Remember, state and substate enums must be a positive integer between 0 and 9999
	
	// list all the primary states first
	child,
	adult,
	elderly,
	
	// list substates in an easy-to-remember way (resetting them to 0 is also nice for easy tracking)
	
	child_baby = 0,
	child_toddler,
	child_teenager,
	
	adult_college = 0,
	adult_job,
	adult_parent,


	elderly_retired = 0,
	elderly_grandparent,
	elderly_motorcycleGangHooligan

	// Look at the bottom of this code for an example of how to set up your state system in the STEP event
}




*/



//// @func adv_state_get([state_int]) No argument: returns state we're in.  With state_int, returns sub_state_int we're in.
function adv_state_get(_state = -1000){
	

	if(_state == -1000)
		{	/// We're asking for the overall internal state
			if(!variable_instance_exists(id,"ADV_STATE")) 
				{
					id.ADV_STATE = 0;
					return 0;
				}
			else return floor(ADV_STATE);
			
		}
	else
	{	// we provided the state - we want the substate
		_state = floor(_state);

		if(!variable_instance_exists(id,"ADV_STATE")) ADV_STATE = _state;
		if(floor(ADV_STATE) != _state) ADV_STATE = _state;
	
		return ((ADV_STATE - _state) * 10000);
	}
}



/// @func adv_state_set(state_int/undefined,[substate_int]) Sets the state to state.substate_int. 
function adv_state_set(_state = undefined,_sub_state_int = 0) {
	
	if(_state == undefined) 
		{
			if(!variable_instance_exists(id,"ADV_STATE")) ADV_STATE = 0;
			
			_state = floor(ADV_STATE);	
		}
	else _state = floor(_state);
	
	ADV_STATE = _state + (_sub_state_int/10000);
	
	return ADV_STATE;
}


/// @func adv_state_external_get(object_id, [state_int]) No supplied state_int: returns object_id's primary state INT. With primary state_int, returns object_id's sub-state INT. 
 function adv_state_external_get(_object_id, _state = -1000){
	 
	 if(instance_exists(_object_id))
		{
			var _val;
			
			with(_object_id)
				{
					_val = adv_state_get(_state);	
				}
			
			return _val;
		}

	else
		{
			show_debug_message("ADV_STATE_EXTERNAL_GET: " + string(_object_id) + " is not a valid instance ID (non-existent or deactivated?");	
			return -1;
		}
 }


/// @func adv_state_external_set(object_id, state_int,[sub_state_int]) Sets object_id's advanced state system to state (state_int) (and optionally: sub_state to sub_state_int)
function adv_state_external_set(_object_id, _state = undefined,_sub_state_int = 0){
	 
	 if(instance_exists(_object_id))
		{
			var _val;
			
			with(_object_id)
				{
					_val = adv_state_set(_state,_sub_state_int);	
				}
			
			return _val;
		}

	else
		{
			show_debug_message("ADV_STATE_EXTERNAL_SET: " + string(_object_id) + " is not a valid instance ID (non-existent or deactivated?");	
			return -1;
		}
 }
 
 
 
 
 /*
 
Template state machine switching:
 
I recommend setting up some enums to make tracking states easier (See above).
	
COPY BELOW INTO OBJECT'S STEP EVENT AND MODIFY AS DESIRED:
----------------------------------------------------------------
	
var _state = adv_state_get();
var _substate = adv_state_get(_state);

switch(_state)
	{
		case state.child:
			{
				switch(_substate)
					{
						case state.child_baby:
							{
								// Code
								
								// If we're ready to leave this substate, move on
								adv_state_set(undefined,state.child_toddler);
								break;
							}
					
						case state.child_toddler:
							{
								// Code
								
								// If we're ready to leave this substate, move on
								adv_state_set(undefined,state.child_teenager);
								break;
							}
					
						case state.child_teenager:
							{
								// Code
								
								// We're totally done with our first state, move to next.
								adv_state_set(state.adult);
								break;
							}
						
					}
			
				break;
			}
	
		case state.adult:
			{
				switch(_substate)
					{
						case state.adult_college:
							{
								// Code
								
								// If we're ready to leave this substate, move on
								adv_state_set(undefined,state.adult_job);
								break;
							}
					
						case state.adult_job:
							{
								// Code
								
								// If we're ready to leave this substate, move on
								adv_state_set(undefined,state.adult_parent);
								break;
							}
					
						case state.adult_parent:
							{
								// Code
								
								// We're totally done with our second state, move to third.
								adv_state_set(state.elderly);
								break;
							}
						
					}
			
				break;
			}
	
	}
	
	
	
	
 
 */
